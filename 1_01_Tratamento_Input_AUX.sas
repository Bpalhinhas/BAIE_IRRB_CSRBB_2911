%MACRO TFixedRate(T_FIXED_RATE_EUR, T_FIXED_RATE_USD, dt_ref);

DATA work.I_FIXED_RATE_EUR;
	SET &T_FIXED_RATE_EUR.(rename=(F2=ID_DESC Instument_ID=ID_OPERACAO_I  Maturity_Date=DATA_FIM 
		Expiration___Repricing_date=DATA_REPRICING Next_Payment_date=DATA_PROX_PAG
		Coupon_rate=TAXA_JURO_CONTRATUAL Tipo_de_cup_o=TIPO_TAXA 
		Payment_frequency=FREQUENCIA_PAGAMENTO Residual_maturity=MATURIDADE_RESIDUAL
		Starting_date=DATA_INICIO Data__ltimo_cup_o=DATA_ULT_PAG 
		Periodicidade_de_Revis_o_de_Taxa=PERIOD_REVISAO_TAXA Revis_o_de_taxa=DT_REVIS_TAXA
		Interest__capital_coupon=MONTANTE_TOTAL));
		MOEDA='EUR';
	RUN;

DATA work.I_FIXED_RATE_USD;
	SET &T_FIXED_RATE_USD.(rename=(F2=ID_DESC Instument_ID=ID_OPERACAO_I  Data_de_Maturidade=DATA_FIM 
		Expiration___Repricing_date=DATA_REPRICING Next_Payment_date=DATA_PROX_PAG
		Coupon_rate=TAXA_JURO_CONTRATUAL Tipo_de_cup_o=TIPO_TAXA 
		Payment_frequency=FREQUENCIA_PAGAMENTO Residual_maturity=MATURIDADE_RESIDUAL
		Starting_date=DATA_INICIO Data__ltimo_cup_o=DATA_ULT_PAG 
		Revis_o_Taxa=PERIOD_REVISAO_TAXA Revis_o_de_taxa=DT_REVIS_TAXA
		Interest__capital_coupon=MONTANTE_TOTAL));
		MOEDA='USD';
	RUN;

DATA WORK.T_FIXED_RATE(KEEP= ID_DESC Client ID_OPERACAO_I DATA_FIM DATA_REPRICING DATA_PROX_PAG  TAXA_JURO_CONTRATUAL TIPO_TAXA
						TIPO_TAXA FREQUENCIA_PAGAMENTO MATURIDADE_RESIDUAL DATA_INICIO DATA_ULT_PAG
						PERIOD_REVISAO_TAXA DT_REVIS_TAXA MONTANTE_TOTAL MOEDA);
	SET work.I_FIXED_RATE_USD work.I_FIXED_RATE_EUR;
	where Client ne "" AND ID_OPERACAO_I ne "";
	DATA_REF = input(&dt_ref.,yymmdd10.);
	format DATA_REF yymmdd10.;
	RUN;

%MEND;

%MACRO TFixedRate_Instrum();

DATA WORK.T_FIXED_RATE_OBRIG;
	SET  WORK.T_FIXED_RATE;
	where ID_DESC = "Títulos de dívida";
	RUN;

DATA WORK.T_FIXED_RATE_CRED;
	SET  WORK.T_FIXED_RATE;
	where (ID_DESC = "Empréstimos e adiantamentos") AND (CLIENT  NOT CONTAINS "Descontos");
	ID_OPERACAO = PUT(input(ID_OPERACAO_I, ?? best12.), Z15.);
	RUN;

DATA WORK.T_FIXED_RATE_DP_DO;
	SET  WORK.T_FIXED_RATE;
	where (ID_DESC = "Depósitos com maturidade definida" OR ID_DESC = "Depósitos sem maturidade definida")
			AND (ID_OPERACAO_I NE "Mapeamento IRRBB");
	RUN;

%MEND;

%MACRO Aux_DO_DP(DO, DP_EUR, DP_USD, dt_ref);

DATA WORK.T_AUX_DO(KEEP=ID_CONTRAPARTE DATA_REF DATA_FIM  DATA_INICIO 
					Tipo_Cliente MATURIDADE_RESIDUAL MOEDA Desc__Produto ID_OPERACAO);
	SET &DO.(rename=(Moeda=MOEDA  Data_Ult__Movimento=DATA_FIM Data=DATA_REF
		N__dias=MATURIDADE_RESIDUAL Data_in_cio=DATA_INICIO));
	where Moeda = "EUR" OR Moeda = "USD";
	ID_CONTRAPARTE = PUT(Cliente, Z10.);
	ID_OPERACAO = PUT(Conta, Z15.);
	RUN;	

DATA WORK.T_AUX_DP;
	SET &DP_EUR. &DP_USD.;
	RUN;

DATA WORK.T_AUX_DP(KEEP=ID_CONTRAPARTE DATA_REF DATA_FIM Stock_Moeda DATA_INICIO 
					Tipo_Cliente Pais TAXA_JURO_CONTRATUAL MATURIDADE_RESIDUAL DATA_REPRICING
					MONTANTE_TOTAL);
	SET WORK.T_AUX_DP(rename=(Moeda=MOEDA  Data_fim=DATA_FIM N__dias=MATURIDADE_RESIDUAL 
					Data_inicio=DATA_INICIO Valor=Stock_Moeda Empresa_Particular=Tipo_Cliente
					Prox_data_repr=DATA_REPRICING Taxa_Juro=TAXA_JURO_CONTRATUAL Nat_=NATUREZA
					Montante___Juro_Total=MONTANTE_TOTAL));
	where Moeda = "EUR" OR Moeda = "USD";
	ID_CONTRAPARTE = PUT(N__conta, Z10.);
	SEQUENCIA = PUT(Seq, Z3.);
	DATA_REF = input(&dt_ref.,yymmdd10.);
	format DATA_REF yymmdd10.;
	RUN;

%MEND;


%MACRO TratamentoLive(LIVE);

DATA AUX.T_LIVE(DROP= ID_CLIENTE SEQUENCIA SEQ ID_OPERACAO_A ID_OPERACAO_I);
	SET &LIVE.;
	WHERE ID_EconScenario=1;
	ID_CLIENTE = scan(ID_Account, 1, '.');
    NATUREZA = scan(ID_Account, 2, '.');
    SEQUENCIA = scan(ID_Account, 3, '.');
	SEQ = INPUT(SEQUENCIA,3.);
	ID_OPERACAO_A = CATS(ID_CLIENTE, NATUREZA, PUT(SEQ,Z3.));
	ID_OPERACAO_I = INPUT(scan(ID_OPERACAO_A, 1, '.'),15.);
	IF INDEX(ID_Account, '.') = 0 THEN ID_OPERACAO=ID_Account;
		ELSE ID_OPERACAO=PUT(ID_OPERACAO_I,Z15.);
	IF INDEX(ID_Account,'22300') THEN ID_OPERACAO=PUT(ID_OPERACAO_I,Z15.);
	IF NATUREZA='90' THEN CONTA_BAL='9000';
	
	RUN;


%MEND;

%MACRO Tratamento_Buckets(BUCKETS);

DATA AUX.T_BUCKETS(KEEP=Inicio Fim Categoria Midpoint);
	SET &BUCKETS.;
	RUN;

%MEND;



%MACRO Construcao_TIPO_AMORT(IN, OUTPUT);

DATA AUX.T_PLANO_FINANCEIRO;
	SET &IN.;
		DATA_EOMONTH=INTNX('MONTH', AMDVNC,0,'END');
		CHAVE=CATS(GBAMCONTA,DATA_EOMONTH, AMSITU, PUT(AMSLD,Z10.), AMTREG);
		RUN;

PROC SQL;
	CREATE TABLE AUX.AUX_CONST_TA AS
	SELECT	
		GBAMCONTA
		,SUM(CASE WHEN ((AMSITU="N") AND (AMTREG="A") AND (AMSLD<0)) THEN 1 ELSE 0 END) AS N_AMORT
		,SUM(CASE WHEN ((AMSITU="N") AND (AMTREG="R") AND (AMVLRC<0)) THEN 1 ELSE 0 END) AS CRIT_RENDAS
		,CASE WHEN (CALCULATED N_AMORT) = 1 THEN MAX(INTNX('MONTH', AMDSIT,0,'END')) END AS MESANO_UNICA_AMORT
		FROM AUX.T_PLANO_FINANCEIRO
		GROUP BY GBAMCONTA;
	QUIT;
	
PROC SORT DATA=AUX.AUX_CONST_TA OUT=&OUTPUT. NODUPRECS;
    BY _ALL_;
RUN;

%MEND;

%MACRO TRATAMENTO_PLANO_FINANCEIRO(IN,OPERACOES,OUT);

PROC SQL NOPRINT;
    SELECT CATS("'", ID_OPERACAO, "'") /* Adiciona aspas simples ao redor de cada ID_OPERACAO */
    INTO :OPERACOES_PF SEPARATED BY ', ' /* Define o delimitador como vírgula e espaço */
    FROM &OPERACOES.;
QUIT;

PROC SORT DATA=&IN. OUT=AUX.PF_SEM_DUPLI NODUPLICATES;
    BY _ALL_;  
RUN;

PROC SQL;
	CREATE TABLE AUX.PLF AS
		SELECT
			GBAMCONTA AS ID_OPERACAO
			,(-AMSLD) AS AMORTIZACAO
        	,(-AMVLRC) AS REGULARIZACAO FORMAT=COMMA12.2 
			,(AMCPDV) AS CAPITAL_REM FORMAT=COMMA12.2 
			,AMDVNC AS DATA_PROJ
			,AMTREG
	FROM AUX.PF_SEM_DUPLI
	WHERE AMSITU = "N" 
	  AND (AMTREG = "A" OR AMTREG = "R")
	  AND GBAMCONTA IN (&OPERACOES_PF.);
QUIT;

DATA &OUT.(KEEP=ID_OPERACAO CF_CAPITAL CAPITAL_REM DATA_PROJ FLG_PLF);
	SET AUX.PLF;
	IF AMTREG="R" THEN AMORTIZACAO=0;
	ELSE REGULARIZACAO=0;
	CF_CAPITAL=AMORTIZACAO+REGULARIZACAO;
	FLG_PLF="Y";
	RUN;

%MEND;